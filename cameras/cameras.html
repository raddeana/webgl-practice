<html>
<head>
  <title>摄像机</title>
</head>
<body>
  <canvas id="canvas"></canvas>
  <script id="envmap-vertex-shader" type="x-shader/x-vertex">
    attribute vec4 a_position;
    attribute vec3 a_normal;

    uniform mat4 u_projection;
    uniform mat4 u_view;
    uniform mat4 u_world;

    varying vec3 v_worldPosition;
    varying vec3 v_worldNormal;

    void main() {
      // Multiply the position by the matrix.
      gl_Position = u_projection * u_view * u_world * a_position;

      // send the view position to the fragment shader
      v_worldPosition = (u_world * a_position).xyz;

      // orient the normals and pass to the fragment shader
      v_worldNormal = mat3(u_world) * a_normal;
    }
  </script>
  <script id="envmap-fragment-shader" type="x-shader/x-fragment">
    precision highp float;

    // Passed in from the vertex shader.
    varying vec3 v_worldPosition;
    varying vec3 v_worldNormal;

    // The texture.
    uniform samplerCube u_texture;

    // The position of the camera
    uniform vec3 u_worldCameraPosition;

    void main() {
      vec3 worldNormal = normalize(v_worldNormal);
      vec3 eyeToSurfaceDir = normalize(v_worldPosition - u_worldCameraPosition);
      vec3 direction = reflect(eyeToSurfaceDir,worldNormal);

      gl_FragColor = textureCube(u_texture, direction);
    }
  </script>
  <script id="skybox-vertex-shader" type="x-shader/x-vertex">
    attribute vec4 a_position;
    varying vec4 v_position;
    void main() {
      v_position = a_position;
      gl_Position = a_position;
    }
  </script>
  <script id="skybox-fragment-shader" type="x-shader/x-fragment">
    precision mediump float;

    uniform samplerCube u_skybox;
    uniform mat4 u_viewDirectionProjectionInverse;

    varying vec4 v_position;
    void main() {
      vec4 t = u_viewDirectionProjectionInverse * v_position;
      gl_FragColor = textureCube(u_skybox, normalize(t.xyz / t.w));
    }
  </script>
  <script src="https://webglfundamentals.org/webgl/resources/webgl-utils.js"></script>
  <script src="https://webglfundamentals.org/webgl/resources/m4.js"></script>
  <script src="https://webglfundamentals.org/webgl/resources/primitives.js"></script>
</body>
</html>
